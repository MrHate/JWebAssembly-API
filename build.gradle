plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'

group 'de.inetsoftware'
archivesBaseName = 'jwebassembly-api'
version = '0.1'

repositories {
    jcenter()
}

dependencies {
    compileOnly     'com.google.code.findbugs:jsr305:3.0.1'
}

sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'src'
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

build {
    dependsOn sourcesJar
    dependsOn javadocJar
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['JWebAssemblyPublication']
    publish = true
    override = true
    pkg {
        repo = 'OSS'
        userOrg = 'i-net-software'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/i-net-software'
        name = group + '.' + archivesBaseName
        version { 
            released = new Date()
        }
    }
}

publishing {
    publications {
        JWebAssemblyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifactId archivesBaseName
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'JWebAssembly-API')
                root.appendNode('description', 'The API for using WebAssembly features in your Java classes.')
                root.appendNode('url', 'https://github.com/i-net-software/JWebAssembly-API')

                def node = root.appendNode('developers').appendNode( 'developer' )
                node.appendNode( 'id', 'Horcrux7' )
                node.appendNode( 'name', 'Volker Berlin' )
                node.appendNode( 'email', 'vberlin@inetsoftware.de' )
                node.appendNode( 'organization', 'i-net software' )
                node.appendNode( 'organizationUrl', 'https://www.inetsoftware.de' )

                node = root.appendNode('scm')
                node.appendNode( 'connection', 'scm:git:git@github.com:i-net-software/JWebAssembly-API.git' )
                node.appendNode( 'developerConnection', 'scm:git:git@github.com:i-net-software/JWebAssembly-API.git' )
                node.appendNode( 'url', 'https://github.com/i-net-software/JWebAssembly-API' )

                root.children().last()
            }
        }
    }
}
